CREATE TABLE dbo.DIM_DATE
(
	DATE_KEY INT
	,DATE DATE
	,DAY TINYINT
	,DAY_SUFFIX CHAR(2)
	,DAY_OF_WEEK_NUMBER TINYINT
	,DAY_OF_WEEK_NAME VARCHAR(10)
	,DAY_OF_WEEK_NAME_ABV CHAR(3)
	,DAY_OF_YEAR SMALLINT
	,WEEK_OF_MONTH TINYINT
	,WEEK_OF_YEAR TINYINT
	,MONTH_NUMBER TINYINT
	,MONTH_NAME VARCHAR(10)
	,MONTH_NAME_ABV CHAR(3)
	,QUARTER_NUMBER TINYINT
	,QUARTER_NAME VARCHAR(6)
	,YEAR INT
	,WEEKEND_IND BIT
	,FIRST_DATE_OF_YEAR DATE
	,LAST_DATE_OF_YEAR	DATE
	,FIRST_DATE_OF_QUATER DATE
	,LAST_DATE_OF_QUATER DATE
	,FIRST_DATE_OF_MONTH DATE
	,LAST_DATE_OF_MONTH DATE
	,FIRST_DATE_OF_WEEK DATE
	,LAST_DATE_OF_WEEK	DATE
);



SET NOCOUNT ON

TRUNCATE TABLE dbo.DIM_DATE

DECLARE @CURRENT_DATE DATE = '1980-01-01'
DECLARE @END_DATE DATE = '2099-12-31'

WHILE @CURRENT_DATE < @END_DATE
BEGIN
	INSERT INTO dbo.DIM_DATE
	(
		DATE_KEY
		,DATE
		,DAY
		,DAY_SUFFIX
		,DAY_OF_WEEK_NUMBER
		,DAY_OF_WEEK_NAME
		,DAY_OF_WEEK_NAME_ABV
		,DAY_OF_YEAR
		,WEEK_OF_MONTH
		,WEEK_OF_YEAR
		,MONTH_NUMBER
		,MONTH_NAME
		,MONTH_NAME_ABV
		,QUARTER_NUMBER
		,QUARTER_NAME
		,YEAR
		,WEEKEND_IND
		,FIRST_DATE_OF_YEAR
		,LAST_DATE_OF_YEAR
		,FIRST_DATE_OF_QUATER
		,LAST_DATE_OF_QUATER
		,FIRST_DATE_OF_MONTH
		,LAST_DATE_OF_MONTH
		,FIRST_DATE_OF_WEEK
		,LAST_DATE_OF_WEEK
	)
   
	SELECT
		DATE_KEY = YEAR(@CURRENT_DATE) * 10000 + MONTH(@CURRENT_DATE) * 100 + DAY(@CURRENT_DATE)
		,DATE = @CURRENT_DATE
		,DAY = DAY(@CURRENT_DATE)
		,DAY_SUFFIX =
			CASE 
				WHEN
					DAY(@CURRENT_DATE) = 1
					OR DAY(@CURRENT_DATE) = 21
					OR DAY(@CURRENT_DATE) = 31
						THEN 'ST'
				WHEN
					DAY(@CURRENT_DATE) = 2
					OR DAY(@CURRENT_DATE) = 22
						THEN 'ND'
				WHEN
					DAY(@CURRENT_DATE) = 3
					OR DAY(@CURRENT_DATE) = 23
						THEN 'RD'
						ELSE 'TH'
				END
		,DAY_OF_WEEK_NUMBER = DATEPART(dw, @CURRENT_DATE)
		,DAY_OF_WEEK_NAME = UPPER(DATENAME(dw, @CURRENT_DATE))
		,DAY_OF_WEEK_NAME_ABV = UPPER(LEFT(DATENAME(dw, @CURRENT_DATE), 3))
		,DAY_OF_YEAR = DATENAME(dy, @CURRENT_DATE)
		,WEEK_OF_MONTH = DATEPART(WEEK, @CURRENT_DATE) - DATEPART(WEEK, DATEADD(MM, DATEDIFF(MM, 0, @CURRENT_DATE), 0)) + 1
		,WEEK_OF_YEAR = DATEPART(wk, @CURRENT_DATE)
		,MONTH_NUMBER = MONTH(@CURRENT_DATE)
		,MONTH_NAME = UPPER(DATENAME(mm, @CURRENT_DATE))
		,MONTH_NAME_ABV = UPPER(LEFT(DATENAME(mm, @CURRENT_DATE), 3))
		,QUARTER_NUMBER = DATEPART(q, @CURRENT_DATE)
		,QUARTER_NAME =
			CASE 
				WHEN DATENAME(qq, @CURRENT_DATE) = 1
					THEN 'FIRST'
				WHEN DATENAME(qq, @CURRENT_DATE) = 2
					THEN 'SECOND'
				WHEN DATENAME(qq, @CURRENT_DATE) = 3
					THEN 'THIRD'
				WHEN DATENAME(qq, @CURRENT_DATE) = 4
					THEN 'FOURTH'
				END
		,YEAR = YEAR(@CURRENT_DATE)
		,WEEKEND_IND =
			CASE 
				WHEN
					DATENAME(dw, @CURRENT_DATE) = 'Sunday'
					OR DATENAME(dw, @CURRENT_DATE) = 'Saturday'
						THEN 1
						ELSE 0
				END
		,FIRST_DATE_OF_YEAR   = CAST(CAST(YEAR(@CURRENT_DATE) AS VARCHAR(4)) + '-01-01' AS DATE)
		,LAST_DATE_OF_YEAR    = CAST(CAST(YEAR(@CURRENT_DATE) AS VARCHAR(4)) + '-12-31' AS DATE)
		,FIRST_DATE_OF_QUATER = DATEADD(qq, DATEDIFF(qq, 0, @CURRENT_DATE), 0)
		,LAST_DATE_OF_QUATER  = DATEADD(dd, - 1, DATEADD(qq, DATEDIFF(qq, 0, @CURRENT_DATE) + 1, 0))
		,FIRST_DATE_OF_MONTH  = CAST(CAST(YEAR(@CURRENT_DATE) AS VARCHAR(4)) + '-' + CAST(MONTH(@CURRENT_DATE) AS VARCHAR(2)) + '-01' AS DATE)
		,LAST_DATE_OF_MONTH   = EOMONTH(@CURRENT_DATE)
		,FIRST_DATE_OF_WEEK   = DATEADD(dd, - (DATEPART(dw, @CURRENT_DATE) - 1), @CURRENT_DATE)
		,LAST_DATE_OF_WEEK = DATEADD(dd, 7 - (DATEPART(dw, @CURRENT_DATE)), @CURRENT_DATE)

	SET @CURRENT_DATE = DATEADD(DD, 1, @CURRENT_DATE)
END




SELECT * FROM  dbo.DIM_Date ORDER BY DATE;